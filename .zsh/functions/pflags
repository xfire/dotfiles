## vim:ft=zsh
##

pflags () {
    emulate -L zsh
    setopt extendedglob

    local -a enabled
    local chk name pformat flags

    zstyle -a ":pflags:-init-" "enable" enabled
    zstyle -a ":pflags:-init-" format pformat

    for chk in $^fpath/pflags_*(N); do
        if [[ $chk == */pflags_(#b)(*) ]]; then
            name="$match[1]"
            [[ -z ${(M)enabled:#${name}} ]] && continue
            if [[ -r "$chk" ]]; then
                autoload -U pflags_${name}
                pflags_${name}
            else
                print "Couldn't read file $chk containing $name."
            fi
        fi
    done


    if [[ -n "${flags}" ]] ; then
        zformat -f pflags_msg ${pformat} f:${flags}
    else
        pflags_msg=
    fi
}

pflags "$*"
